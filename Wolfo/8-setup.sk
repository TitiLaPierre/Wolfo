#
# This file is part of Wolfo.
#
# Wolfo is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.
#
# Wolfo is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with Wolfo.  If not, see <https://www.gnu.org/licenses/>.
#
# Copyright 2020 - Romitou <romitou@protonmail.com> and contributors
#

on script load:
	send join "&3&lWolfo &7&l▪ &8Took %difference between now and {wolfo::loading}%." to ops
	send "" to ops
	send "&3&lWolfo &7&l▪ &7Reloading setup ..." to ops
	set {wolfo::loading} to now

function wolfoMenuAction(p: player, loop: integer, action: text):
	close {_p}'s inventory
	if {_action} is "MANAGE":
		if {_loop} is 11:
			wolfoCheckStart({_p})
		else if {_loop} is 13:
			wolfoSetText("GAME.PRIVACY", "CLOSED")
			send join wolfoGetText("PREFIX.GENERAL", 1) and wolfoGetText("MESSAGE.PRIVACY.CLOSED", 2, {_p}) to {wolfo::players::list::*}
		else if {_loop} is 14:
			wolfoSetText("GAME.PRIVACY", "WHITELISTED")
			send join wolfoGetText("PREFIX.GENERAL", 1) and wolfoGetText("MESSAGE.PRIVACY.WHITELISTED", 2, {_p}) to {wolfo::players::list::*}
		else if {_loop} is 15:
			wolfoSetText("GAME.PRIVACY", "OPEN")
			send join wolfoGetText("PREFIX.GENERAL", 1) and wolfoGetText("MESSAGE.PRIVACY.OPEN", 2, {_p}) to {wolfo::players::list::*}
	else:
		if {_loop} is 11:
			wolfoSetLoc("GAME.LOCATIONS.MIDDLE", position of {_p})
			send join wolfoGetText("PREFIX.GENERAL", 1) and wolfoGetText("MESSAGE.LOCATIONSET", 2, {_p}) to {_p}
			wolfoUpdateO("GAME.SETUP.STEPS", "SETUP.MIDDLE", 0)
		else if {_loop} is 12:
			set {_pos} to size of node list "GAME.LOCATIONS.SPAWNPOINTS" from "config"
			wolfoSetLoc("GAME.LOCATIONS.SPAWNPOINTS.%{_pos}%", position of {_p})
			send join wolfoGetText("PREFIX.GENERAL", 1) and wolfoGetText("MESSAGE.LOCATIONSETO", 2, {_p}) to {_p}
			if {_pos} is higher than 4:
				wolfoGetObjects("GAME.SETUP.STEPS", 1) do not contain "SETUP.SPAWNPOINT"
				wolfoUpdateO("GAME.SETUP.STEPS", "SETUP.SPAWNPOINT", 0)
		else if {_loop} is 14:
			wolfoSetLoc("GAME.LOCATIONS.BEGIN", position of {_p})
			send join wolfoGetText("PREFIX.GENERAL", 1) and wolfoGetText("MESSAGE.LOCATIONSET", 2, {_p}) to {_p}
			wolfoUpdateO("GAME.SETUP.STEPS", "SETUP.BEGIN", 0)		
		else if {_loop} is 15:
			wolfoSetLoc("GAME.LOCATIONS.END", position of {_p})
			send join wolfoGetText("PREFIX.GENERAL", 1) and wolfoGetText("MESSAGE.LOCATIONSET", 2, {_p}) to {_p}
			wolfoUpdateO("GAME.SETUP.STEPS", "SETUP.END", 0)
		set {_size} to size of wolfoGetObjects("GAME.SETUP.STEPS", 1)
		if size of wolfoGetObjects("GAME.SETUP.STEPS", 1) is 4:
			wolfoSetText("GAME.SETUP.FINISH", "finished")
			send join wolfoGetText("PREFIX.GENERAL", 1) and wolfoGetText("MESSAGE.SETUP_FINISHED2", 2, {_p}) to {_p}
			wolfoDraw("center", "")