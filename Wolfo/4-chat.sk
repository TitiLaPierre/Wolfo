#
# This file is part of Wolfo.
#
# Wolfo is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.
#
# Wolfo is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with Wolfo.  If not, see <https://www.gnu.org/licenses/>.
#
# Copyright 2020 - Romitou <romitou@protonmail.com> and contributors
#

on script load:
	send join "&3&lWolfo &7&l▪ &8Took %difference between now and {wolfo::loading}%." to ops
	send "" to ops
	send "&3&lWolfo &7&l▪ &7Loading chat ..." to ops
	set {wolfo::loading} to now

on chat:
	replace all "&" with "§" in message if player is op
	cancel event
	wolfoDebug("%player% send a message.")
	if {wolfo::game::start} is set:
		if {wolfo::players::life::%player%} is "DEAD":
			loop {wolfo::players::list::*}:
				{wolfo::players::life::%loop-value%} is "DEAD"
				set {_stat} to wolfoGetText("MESSAGE.LIFE.DEAD", loop-value)
				send "&8[&7%{_stat}%&8] &7%player% &8» &7%message%" to loop-value
		else:
			if {wolfo::game::status} is "NIGHT":
				if {wolfo::players::role_name::%player%} is {wolfo::vote::current}:
					loop {wolfo::role::list::%{wolfo::vote::current}%::*}:
						set {_role} to wolfoGetText("ROLES.%{wolfo::vote::current}%.NAME", loop-value)
						set {_day} to wolfoGetText("MESSAGE.TIME.%{wolfo::game::status}%", loop-value)
						send "&8[&7%{_day}%s&8] &8[%{_role}%&8] &7%player% &8» &7%message%" to loop-value
				else:
					send join wolfoGetText("PREFIX.GENERAL") and wolfoGetText("MESSAGE.MUTED", player)
			else:
				loop {wolfo::players::list::*}:
					set {_day} to wolfoGetText("MESSAGE.TIME.%{wolfo::game::status}%", loop-value)
					send "&8[&f%{_day}%&8] &7%player% &8» &7%message%" to loop-value
	else:
		send "&7%player% &8» &7%message%" to {wolfo::players::list::*}
